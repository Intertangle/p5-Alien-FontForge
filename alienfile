use alienfile;

plugin 'PkgConfig' => 'libfontforge';

requires 'Alien::Build::Plugin::Gather::Dino';
requires 'Path::Tiny' => 0;

# https://fontforge.github.io/
share {
	# Use .zip on Windows' MSYS2 because of extraction issues with bsdtar under MSYS2
	my $ext = exists $ENV{MSYSTEM} ? 'zip' : 'tar.gz';
	plugin Download => (
		url => 'https://github.com/fontforge/fontforge/releases',
		version => qr/(\d{4}\d{2}\d{2})\.\Q$ext\E/,
	);

	if ( exists $ENV{MSYSTEM} ) {
		plugin 'Extract' => $ext;
	} else {
		plugin 'Extract::CommandLine' => $ext;
	}

	patch sub {
		my($splinefont_h) = Path::Tiny->new('fontforge/splinefont.h');

		$splinefont_h->edit_lines(sub {
			s{\Q#include "locale.h"\E}{#include <locale.h>\n#include <xlocale.h>\n}g;
		});
	};

	plugin 'Build::Autoconf' => ();

	build [
		[ 'sh ./bootstrap' ],
		[ '%{configure}'
			. ' ' . join(' ',
				qw(--disable-programs
				--disable-python-scripting
				--disable-python-extension
				--without-x
				)
			)
		],
		[ '%{make}' ],
		[ '%{make}', 'install' ],
	];

	plugin 'Gather::Dino';
};
